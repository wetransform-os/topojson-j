plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.wetransform-os'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url 'https://repo.osgeo.org/repository/release/'
    }
    mavenCentral()
}

dependencies {
    implementation 'org.lz4:lz4-java:1.4.1' // Apache 2.0
    implementation 'com.github.albfernandez:javadbf:1.13.2' // LGPL 3.0
    implementation 'com.google.code.gson:gson:2.9.1' // Apache 2.0
    implementation 'org.apache.commons:commons-lang3:3.12.0' // Apache 2.0

    // GeoTools (LGPL 2.1)
    implementation 'org.geotools:geotools:21.0'
    implementation 'org.geotools:gt-epsg-hsql:21.0'
    implementation 'org.geotools:gt-opengis:21.0'
    implementation 'org.geotools:gt-geometry:21.0'
    implementation 'org.geotools:gt-referencing:21.0'

    testImplementation 'junit:junit:4.13'
}

java {
    withSourcesJar()
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }
    test {
        java {
            srcDirs 'unittest'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url = project.version.endsWith('-SNAPSHOT') ?
                'https://artifactory.wetransform.to/artifactory/libs-snapshot-local' :
                'https://artifactory.wetransform.to/artifactory/libs-release-local'
            credentials {
                username project.hasProperty('wetfArtifactoryUser') ? wetfArtifactoryUser : ''
                password project.hasProperty('wetfArtifactoryPassword') ? wetfArtifactoryPassword : ''
            }
        }
    }
}

wrapper {
    gradleVersion = '7.5.1'
    distributionType = Wrapper.DistributionType.ALL
}
