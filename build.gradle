plugins {
    id 'java-library'
    id 'maven-publish'
    id 'to.wetransform.semantic-release-version' version '2.1.2'
}

group = 'com.github.wetransform-os'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
    maven {
        url 'https://repo.osgeo.org/repository/release/'
    }
    mavenCentral()
}

ext {
    geotoolsVersion = '32.1'
}

dependencies {
    implementation 'org.lz4:lz4-java:1.8.0' // Apache 2.0
    implementation 'com.github.albfernandez:javadbf:1.14.1' // LGPL 3.0
    implementation 'com.google.code.gson:gson:2.11.0' // Apache 2.0
    implementation 'org.apache.commons:commons-lang3:3.17.0' // Apache 2.0

    // GeoTools (LGPL 2.1)
    implementation "org.geotools:gt-main:${geotoolsVersion}"
    implementation "org.geotools:gt-epsg-hsql:${geotoolsVersion}"
    implementation "org.geotools:gt-api:${geotoolsVersion}"
//    implementation "org.geotools:gt-geometry:${geotoolsVersion}"
    implementation "org.geotools:gt-referencing:${geotoolsVersion}"

	implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.9'
	
    testImplementation 'junit:junit:4.13.2'
	
}

java {
    withSourcesJar()
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }
    test {
        java {
            srcDirs 'unittest'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = project.version.endsWith('-SNAPSHOT') ?
                'https://artifactory.wetransform.to/artifactory/libs-snapshot-local' :
                'https://artifactory.wetransform.to/artifactory/libs-release-local'
            credentials {
                username project.hasProperty('wetfArtifactoryUser') ? wetfArtifactoryUser : ''
                password project.hasProperty('wetfArtifactoryPassword') ? wetfArtifactoryPassword : ''
            }
        }
    }
}

wrapper {
    gradleVersion = '8.11.1'
    distributionType = Wrapper.DistributionType.ALL
}
